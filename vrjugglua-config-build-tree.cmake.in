# CMake cross-platform build system
# 2009-2010 Ryan Pavlik <rpavlik@iastate.edu>
# http://academic.cleardefinition.com/
# Iowa State University HCI Graduate Program/VRAC

set(VRJUGGLUA_LIBRARIES vrjlua_buildtree_vrjugglua)
set(VRJUGGLUA_FLTK_CONSOLE_LIBRARIES vrjlua_buildtree_vrjugglua-fltk)
set(VRJUGGLUA_QT_CONSOLE_LIBRARIES vrjlua_buildtree_vrjugglua-qt)

set(VRJUGGLUA_BASE_DIR "@CMAKE_CURRENT_BINARY_DIR@")

set(VRJUGGLUA_INCLUDE_DIRS
	"@CMAKE_CURRENT_SOURCE_DIR@/third-party/lua-5.1.4/src"
	"@CMAKE_CURRENT_SOURCE_DIR@/third-party/Fl_Native_File_Chooser-0.86"
	"@CMAKE_CURRENT_SOURCE_DIR@/third-party/luabind"
	"@CMAKE_CURRENT_SOURCE_DIR@/third-party/osgLua/include"
	"@CMAKE_CURRENT_SOURCE_DIR@/third-party/osgLua/src"
	"@CMAKE_CURRENT_SOURCE_DIR@"
	"@CMAKE_CURRENT_BINARY_DIR@")

set(VRJUGGLUA_VRJ30 @BUILD_WITH_VRJ30@)

if(			(NOT BOOST_INCLUDEDIR)
		AND (NOT BOOST_LIBRARYDIR)
		AND (NOT BOOST_ROOT)
		AND (NOT BOOSTROOT))

	set(BOOST_LIBRARYDIR @Boost_LIBRARY_DIRS@)
	set(BOOST_INCLUDEDIR @Boost_INCLUDE_DIR@)
endif()

function(install_vrjugglua_lua_files)
	install(DIRECTORY "${VRJUGGLUA_BASE_DIR}/share/vrjugglua/lua/"
		DESTINATION "share/vrjugglua/lua/"
		${ARGN})
endfunction()

include("@CMAKE_CURRENT_BINARY_DIR@/vrjugglua-targets.cmake")

if(TARGET vrjlua_buildtree_luac AND NOT TARGET luac)
	# Create a luac target to match the one we imported, so LuaTargets.cmake works
	add_executable(luac IMPORTED)
	get_property(_luac_configs TARGET vrjlua_buildtree_luac PROPERTY IMPORTED_CONFIGURATIONS)
	set_property(TARGET luac PROPERTY IMPORTED_CONFIGURATIONS ${_luac_configs})
	foreach(_config ${_luac_configs})
		get_property(_luac_location TARGET vrjlua_buildtree_luac PROPERTY IMPORTED_LOCATION_${_config})
		set_property(TARGET luac PROPERTY IMPORTED_LOCATION_${_config} ${_luac_location})
	endforeach()
endif()


set(VRJUGGLUA_FOUND ON)
